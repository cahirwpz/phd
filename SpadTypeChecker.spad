)abbrev package STREMREF SpadTreeRemoveReferences
SpadTreeRemoveReferences(find : (PI, PI) -> TN) : SpadTreeWalkerCategory == Implementation where
  PF ==> PrintableForm
  PI ==> PositiveInteger
  N ==> SpadNode
  APP ==> SpadApply(N)
  FT ==> SpadFunctor(N)
  NR ==> SpadNodeRef(N)
  TO ==> SpadTypeOrigin(N)
  TS ==> SpadTypeSelect(N)
  TV ==> SpadTypeVar(N)
  TVL ==> SpadTypeValue(N)
  TN ==> SpadTypeNode

  Implementation ==> add
    import Logger('Fill)
    import FT

    rewrite (to : TO) : N ==
      (e, t) := (rewrite to.expr, rewrite to.type)
      t = ['_$] => e
      t = appform(find(1, 1).node :: FT) => e
      undefinedType? t => e
      mappingType? t => e
      nodeTypeOrigin(e, t)

    rewrite (tv : TVL) : N ==
      rewrite tv.type

    rewrite (app : APP) : N ==
      -- WORKAROUND! Remove origin qualifier from first argument of implicit
      -- call to 'elt' added in walkApp $ SpadTypeChecker.
      fun := rewrite app.function
      args := [rewrite a for a in app.args]
      typeSelect? fun =>
        ts : TS := coerce(rewrite fun)
        nodeTypeSelect(nodeApp(ts.expr, args), ts.type)
      if typeOrigin? fun then 
        to : TO := coerce(fun)
        if to.expr = ['elt] and #args > 0 and typeOrigin? first args then
          to := coerce(first args)
          args := cons(to.expr, rest args)
      nodeApp(fun, args)

    rewrite (tv : TV) : N ==
      to : TO
      tn := find(tv.major :: PI, tv.minor :: PI)
      t := tn.type
      while typeOrigin? t repeat
        to := coerce(t)
        t := to.type
      t

    rewrite (nr : NR) : N ==
      tn := find(nr.major :: PI, nr.minor :: PI)
      tn.node = [nr] =>
        fail pile ["Self reference detected:" :: PF, tn :: PF]
        error ""
      rewrite tn.node

)abbrev package STTCHECK SpadTypeTreeChecker
SpadTypeTreeChecker() : Exports == Implementation where
  NNI ==> NonNegativeInteger
  PF ==> PrintableForm
  PI ==> PositiveInteger

  N ==> SpadNode
  APP ==> SpadApply(N)
  ASS ==> SpadAssign(N)
  SC ==> SpadCase(N)
  CE ==> SpadCondExpr(N)
  ENV ==> SpadEnvironment
  FN ==> SpadFunction(N)
  MT ==> SpadMappingType(N)
  NR ==> SpadNodeRef(N)
  SEQ ==> SpadSequence(N)
  ST ==> SpadSubType(N)
  TC ==> SpadTypeCoerce(N)
  TEI ==> SpadTypeIs(N)
  TO ==> SpadTypeOrigin(N)
  TS ==> SpadTypeSelect(N)
  TV ==> SpadTypeVar(N)
  UT ==> SpadUnionType(N)

  SUBS ==> AssociationList(TV, List(N))
  URES ==> Union(SUBS, "failed")
  SIG ==> Record(sig : N, subs : SUBS)

  PR ==> Record(a : NNI, b : NNI)
  WR ==> Union("progress", "stuck", "error", "done")

  TT ==> SpadTypeTree
  TNA ==> SpadTypeNodeArray(SpadTypeTree)
  TN ==> SpadTypeNode
  TR ==> SpadTypeRule

  Exports ==> with
    walk : TT -> Boolean

  Implementation ==> add
    import Logger('Check)
    import SpadNodeFactory
    import SpadNodeTools
    import SpadTypeUnifier
    import SpadTypeNode
    import SpadTypeTree
    import SpadTypeNodeArray(SpadTypeTree)

    walk' : (TT, PI) -> Boolean

    coerce (s : SIG) : PF ==
      empty? s.subs =>
        bold(s.sig :: PF)
      spaces [bold(s.sig :: PF), bold cyan("with" :: PF), bold(s.subs :: PF)]

    dropSubTypeSignatures(sl : List(SIG)) : List(SIG) ==
      tl : List(SIG) := []
      for s in sl repeat
        superTypeExists? := false
        for t in sl | s.sig ~= t.sig and not superTypeExists? repeat
          if isSubType(s.sig, t.sig) case "true" then
            superTypeExists? := true
        if not superTypeExists? then
          tl := [s, :tl]
      tl

    evalApply(ctx : TT, r : APP, n : TN) : Boolean ==
      funNode := node(ctx, r.function :: NR)
      argNodeList := [node(ctx, arg :: NR) for arg in r.args]

      candidates : List(SIG) := []

      for t in typeOf(ctx, funNode) repeat
        -- 1) Consider signature types with correct number of arguments.
        t' := stripOrigin t
        not mappingType? t' => "iterate"
        sig := t' :: MT
        -- replace {constant -> Type} with {() -> Type}
        if sig.args = [['constant]] then
          sig := [[], sig.result]$MT
        #sig.args ~= #argNodeList => "iterate"
        -- 2) Check if arguments can be applied to function of given
        --    signature.
        debug ["Checking signature:" :: PF, bold(t :: PF)]
        subs : List(SUBS) := []
        argsOk? := true
        for arg in sig.args for narg in argNodeList | argsOk? repeat
          argTypes := typeOf(ctx, narg)
          ures := unifyType(arg, argTypes)
          ures case SUBS =>
            subs := [ures :: SUBS, :subs]
          "and"/[isSubType(argType, arg) case "false" for argType in argTypes] => 
            argsOk? := false
        not argsOk? => "iterate"
        -- 3) Check if result type is correct for given signature.
        resOk? := true
        if sig.result ~= undefinedType then
          res := sig.result
          resTypes := typeOf(ctx, n)
          ures := unifyType(res, resTypes)
          ures case SUBS =>
            subs := [ures :: SUBS, :subs]
          resOk? := false
        not resOk? => "iterate"
        -- 4) Collect valid signature.
        candidates := [[t, merge subs], :candidates]

      candidates := dropSubTypeSignatures candidates

      empty? candidates =>
        info ["No valid signatures found!" :: PF]
        false

      info pile ["Valid signatures:" :: PF, :[coerce c for c in candidates]]

      if typeVar? funNode.type then
        limitNodeType!(ctx, funNode, [c.sig for c in candidates])

      for s in entries merge [c.subs for c in candidates] repeat
        limitTypeVar!(ctx, s.key, s.entry)

      -- Fetch all inferred mappings.
      mappings : List(MT) := []
      for t in [c.sig for c in candidates] repeat
        t := stripOrigin t
        not mappingType? t => "iterate"
        mappings := [t :: MT, :mappings]
      -- Propagate function arguments and return type down the tree.
      for i in 1..#argNodeList for narg in argNodeList repeat
        limitNodeType!(ctx, narg, [(mt.args)(i) for mt in mappings])
      limitNodeType!(ctx, n, [mt.result for mt in mappings]) 
      true

    evalAssign(ctx : TT, r : ASS, n : TN) : Boolean ==
      ln := node(ctx, r.lval :: NR) -- left node
      rn := node(ctx, r.rval :: NR) -- right node
      -- a) {%s[?]} := {%t[t1, t2, ...] | T}
      -- given a type information flows from nodes above during TT
      -- construction and we don't know anything, let's accept types
      -- from below
      hasUnknownType?(ctx, ln) => limitNodeType!(ctx, ln, [rn.type])
      -- b) {%s[s1, s2, ...] | S} := {%t[?]}
      -- we simply don't know, wait for types to flow from below
      hasUnknownType?(ctx, rn) => true
      -- c) {%s[s1, s2, ...] | S} := {%t[t1, t2, ...] | T}
      -- now it's easy...
      not nodeSubType!(ctx, rn, ln) and not nodesTypeMatch!(ctx, ln, rn) => false
      limitNodeType!(ctx, n, [ln.type])

    evalCase(ctx : TT, sc : SC, n : TN) : Boolean ==
      un := node(ctx, sc.expr :: NR) -- union node
      vn := node(ctx, sc.type :: NR) -- variant node
      -- a) {%u[?]} case {%v[v1, v2, ...] | V}
      --    {%u[u1, u2, ...] | U} case {%v[?]}
      -- wait for a union type / variant to propagate up
      hasUnknownType?(ctx, un) or hasUnknownType?(ctx, vn) => true
      -- b) {%u[u1, u2, ...] | U} case {%v[v1, v2, ...] | V}
      us : List(N) := []
      vs : List(N) := []
      for u in typeOf(ctx, un) | unionType? u repeat
        for v in typeOf(ctx, vn) repeat
          -- BUG: move variant? into if statement to reproduce
          variant? := 
            typeValue? v or apply? v =>
              -- select variant by type
              hasVariant? (u :: UT, stripType v)
            typeOrigin? v =>
              -- select variant by name
              -- the origin of variant must match with the type of union
              (unify?(u, (v :: TO).type) and
               hasVariant? (u :: UT, [stripOrigin(v) :: String :: Symbol]))
            false
          if variant? then
            us := [u, :us]
            vs := [stripType v, :vs]
      us := removeDuplicates us
      vs := removeDuplicates vs
      setTypeOf!(ctx, un, us) and setTypeOf!(ctx, vn, vs)

    evalCondExpr(ctx : TT, r : CE, n : TN) : Boolean ==
      cn := node(ctx, r.cond :: NR) -- condition node
      not limitNodeType!(ctx, cn, [booleanType]) => false

      tn := node(ctx, r.truebr :: NR) -- true branch node
      emptyNode? r.falsebr =>
        hasUndefinedType?(ctx, tn) =>
          bindNode!(ctx, n, undefinedType)
          bindNode!(ctx, tn, undefinedType)
          done! n
          done! tn
          true
        limitNodeType!(ctx, n, [tn.type, nodeUnionType [tn.type, undefinedType]])

      fn := node(ctx, r.falsebr :: NR) -- false branch node

      hasUndefinedType?(ctx, fn) and hasUndefinedType?(ctx, tn) =>
        bindNode!(ctx, n, undefinedType)
        done! n
        true

      hasUndefinedType?(ctx, tn) =>
        limitNodeType!(ctx, n, [fn.type])

      hasUndefinedType?(ctx, fn) =>
        limitNodeType!(ctx, n, [tn.type])

      t1 := tn.type
      t2 := fn.type
      ts : List(N) :=
        t1 = t2 => [t1]
        isSubType(t1, t2) case "true" => [t2]
        isSubType(t2, t1) case "true" => [t1]
        dropSubTypes [t1, t2, nodeUnionType [t1, t2], nodeUnionType [t2, t1]]

      not limitNodeType!(ctx, n, ts) => false

      ts := typeOf(ctx, n)

      nodeSubTypeOf!(ctx, tn, ts) and nodeSubTypeOf!(ctx, fn, ts)

    evalTypeIs(ctx : TT, r : TEI, n : TN) : Boolean ==
      sn := node(ctx, r.expr :: NR)
      tn := node(ctx, r.type :: NR)
      -- a) {%s[?]} is {%t[%undef]} | {%s[%undef]} is {%t[?]}
      ---   do not propagate %undef up or down
      hasUndefinedType?(ctx, sn) => true
      hasUndefinedType?(ctx, tn) => true
      -- b) {%s[?]} is {%t[t1, t2, ...] | T}
      --    propagate the type down
      hasUnknownType?(ctx, sn) => setTypeOf!(ctx, sn, [tn.type])
      -- c) {%s[s1, s2, ...] | S} is {%t[?]}
      --    propagate the type up
      hasUnknownType?(ctx, tn) => setTypeOf!(ctx, tn, [sn.type])
      nodesTypeMatch!(ctx, sn, tn)

    evalSubType(ctx : TT, r : ST, n : TN) : Boolean ==
      sn := node(ctx, r.expr :: NR)
      tn := node(ctx, r.type :: NR)
      -- a) {%s[?]} is {%t[%undef]} | {%s[%undef]} is {%t[?]}
      ---   do not propagate %undef up or down
      hasUndefinedType?(ctx, sn) => true
      hasUndefinedType?(ctx, tn) => true
      -- b) {%s[?]} <: {%t[t1, t2, ...] | T} | {%s[s1, s2, ...] | S} <: {%t[?]}
      --    wait for a subtype to propagate up or down
      hasUnknownType?(ctx, sn) or hasUnknownType?(ctx, tn) => true
      nodeSubType!(ctx, sn, tn)

    evalTypeCoerce(ctx : TT, r : TC, n : TN) : Boolean ==
      sn := node(ctx, r.expr :: NR)
      tn := node(ctx, r.type :: NR)
      hasUndefinedType?(ctx, sn) => true
      not nodeSubType!(ctx, tn, sn) => false
      limitNodeType!(ctx, n, [stripType t for t in typeOf(ctx, tn)])

    evalTypeOrigin(ctx : TT, r : TO, n : TN) : Boolean ==
      fn := node(ctx, r.expr :: NR)
      tn := node(ctx, r.type :: NR)
      ts : List(N) := []
      for f in typeOf(ctx, fn) repeat
        if typeOrigin? f then
          to := f :: TO
          if unify?(tn.type, to.type) then
            ts := [f, :ts]
      ts := reverse ts
      empty? ts => false
      limitNodeType!(ctx, n, ts) and limitNodeType!(ctx, fn, ts)

    evalTypeSelect(ctx : TT, r : TS, n : TN) : Boolean ==
      sn := node(ctx, r.expr :: NR)
      tn := node(ctx, r.type :: NR)
      hasUndefinedType?(ctx, sn) => true
      ts : List(N) := []
      for t in typeOf(ctx, tn) repeat
        for s in typeOf(ctx, sn) repeat
          s' := stripOrigin s
          t' := stripType t
          if mappingType? s' then
            mt := s' :: MT
            if unify?(mt.result, t') then
              ts := [s, :ts]
      empty? ts => false
      limitNodeType!(ctx, n, ts) and nodesTypeMatch!(ctx, n, sn)


    -- evaluates single rule; note that it can happen that nothing was changed
    -- even if no type error was detected (result "true" means yes or maybe)
    evalFormula(ctx : TT, f : N, n : TN) : Boolean ==
      emptyNode? f => true
      nodeRef? f => true

      debug (["Running type inference for" :: PF, bold(n.index :: PF),
              "of" :: PF, bold(n.type :: PF), "type." :: PF])

      apply? f => evalApply(ctx, f :: APP, n)
      assign? f => evalAssign(ctx, f :: ASS, n)
      case? f => evalCase(ctx, f :: SC, n)
      condExpr? f => evalCondExpr(ctx, f :: CE, n)
      subType? f => evalSubType(ctx, f :: ST, n)
      typeIs? f => evalTypeIs(ctx, f :: TEI, n)
      typeCoerce? f => evalTypeCoerce(ctx, f :: TC, n)
      typeOrigin? f => evalTypeOrigin(ctx, f :: TO, n)
      typeSelect? f => evalTypeSelect(ctx, f :: TS, n)

      fail ["evalFormula: case not handled for" :: PF, f :: PF]
      error "Internal error!"

    evalAnyRule(ctx : TT, n : TN) : Boolean ==
      rules := n.rules
      empty? rules => true
      while not empty? rules repeat
        fs := (first rules).formulas
        while not empty? fs repeat
          not evalFormula(ctx, first fs, n) => break
          fs := rest fs
        empty? fs => break
        rules := rest rules
      empty? rules => 
        poisonNode!(ctx, n)
        false
      n.rules := rules
      -- Immediately remove rules that fail to apply.
      for victim in setDifference(references n.rules, references rules) repeat
        killNode!(ctx, node(ctx, victim))
      true

    checkAndMarkIfDone! (ctx : TT, n : TN) : Boolean ==
      done? n => true
      killed? n or poisoned? n => false
      unbound? n.type => false
      nodeRef? n.node =>
        checkAndMarkIfDone!(ctx, node(ctx, n.node :: NR)) =>
          done! n
          return true
        return false
      if not empty? n.rules then
        -- We only care about first rule, because it produced actual solution.
        rule := first n.rules
        for ref in references rule repeat
          if not done? node(ctx, ref :: NR) then
            return false
        -- Remove leftovers from other alternative rules.
        for victim in setDifference(references n.rules, references rule) repeat
          killNode!(ctx, node(ctx, victim))
        n.rules := [rule]
        n.node := rule.solution
      done! n; true

    countProgress (ctx : TT, i : PI) : PR ==
      -- a = number of type variables and nodes to be processed
      --     (empty type variable counts twice)
      -- b = sum of types assigned to each type variable
      x := members(ctx).i
      p := [#listTypeVars(x), 0]
      for n in members x repeat
        done? n or killed? n => "iterate"
        p.a := p.a + 1
        if hasUnknownType?(ctx, n)
          then p.a := p.a + 1
          else p.b := p.b + #typeOf(ctx, n)
      p

    walk' (ctx : TT, i : PI) : WR ==
      q : Queue(TN) := empty()

      for n in listPostOrder(ctx, i) | pending? n repeat 
        enqueue!(n, q)

      empty? q => "done"

      p := countProgress(ctx, i)
      step := 1 :: NNI

      while p.a > 0 or p.b > 0 repeat
        newQ : Queue(TN) := empty()

        info ["Processing order:" :: PF, bracket [n.index :: PF for n in members q]]

        -- perform meta-step
        while not empty? q repeat
          n := dequeue! q
          done? n => "iterate"
          poisoned? n => return "error"

          --debug([number(step) :: PF, "Queue state" :: PF, "=>" :: PF,
          --       n.index :: PF, ":" :: PF,
          --       bracket [m.index :: PF for m in members q]])

          evalAnyRule(ctx, n)

          -- Check if we're done with processing type variable tied to this node.
          rewriteIfInferred!(ctx, n)

          -- If type is inferred for all formulas the node itself won't change
          checkAndMarkIfDone!(ctx, n) =>
            debug(["Processing node" :: PF, bold(n.index :: PF),
                   "completed!" :: PF])

          enqueue!(n, newQ)

        -- check if during meta-step the problem was reduced compared to
        -- previous meta-step
        newP := countProgress(ctx, i)
        progress? :=
          newP.a < p.a => true
          newP.a = p.a and newP.b < p.b => true
          false
        not progress? =>
          step = 1 => return "stuck"
          return "progress"

        -- setup new meta-step
        p := newP
        q := newQ
        step := step + 1

      info(["Type checking completed in" :: PF,
            bold(number(i :: Integer) :: PF), "tree !" :: PF])
      "done"

    walk ctx ==
      info vconcat ["Tree ready for type checking..." :: PF, ctx :: PF]

      for na in members ctx repeat
        for n in members na repeat 
          rewriteIfInferred!(ctx, n)

      continue? := true
      success? := false
      failure? := false
      count := #(members ctx)

      while continue? and not (success? or failure?) repeat
        status := [walk'(ctx, k :: PI) for k in 1..count]
        continue? := any?((x : WR) : Boolean +-> x = "progress", status)
        success? := every?((x : WR) : Boolean +-> x = "done", status)
        failure? := any?((x : WR) : Boolean +-> x = "error", status)

      info(["Finished type checking with" :: PF,
            bold(success? => green("success" :: PF); red("failure" :: PF)),
            "!" :: PF])

      if success? then -- Rewrite the tree if succeeded !
        -- capture x in closure, so that we don't need to expose SpadTypeTree 
        -- type to SpadTreeFillInTypes
        for na in members ctx repeat
          findTypeNode := ((i : PI, j : PI) : TN +-> node(ctx, i, j))
          root(na).node := rewrite(root(na).node)$SpadTreeRemoveReferences(findTypeNode)
      else
        for na in reverse members ctx repeat
          for n in reverse members na repeat 
            rewriteIfInferred!(ctx, n)
            checkAndMarkIfDone!(ctx, n)

      debug (ctx :: PF)

      success?
