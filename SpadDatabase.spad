)abbrev domain SPCMM SpadCtorModeMap
SpadCtorModeMap() : Exports == Implementation where
  N ==> SpadNode
  NT ==> SpadNamedType(N)
  PF ==> PrintableForm

  Exports ==> Join(CoercibleTo(SpadNode), CoercibleTo(PrintableForm)) with
    convert : SExpression -> %

    elt : (%, "name") -> Symbol
    elt : (%, "type") -> N
    elt : (%, "args") -> List(NT)

  Implementation ==> add
    Rep := Record(ctor : List(SExpression), unused : SExpression)

    import SExpression
    import SpadNode
    import Printer

    convert x ==
      x pretend %

    elt (x, "name") ==
      symbol car x.ctor.1 
    elt (x, "type") ==
      parse x.ctor.2
    elt (x, "args") ==
      ts := [parse t for t in rest(x.ctor, 2)]
      as := [symbol a for a in destruct cdr x.ctor.1]
      [[a, t]$NT for a in as for t in ts]

    coerce x ==
      hconcat [x.name :: PF, paren [arg :: PF for arg in x.args], ":" :: PF, x.type :: PF]

)abbrev domain SPCC SpadCtorCategory
SpadCtorCategory() : Exports == Implementation where
  N ==> SpadNode
  PF ==> PrintableForm

  Exports ==> Join(CoercibleTo(SpadNode), CoercibleTo(PrintableForm)) with
    convert : SExpression -> %

  Implementation ==> add
    convert x == x pretend %

    coerce (x : %) : N ==
      parse (x pretend SExpression)
    coerce (x : %) : PF ==
      (x :: N) :: PF

)abbrev domain SPOP SpadOperation
SpadOperation() : Exports == Implementation where
  N ==> SpadNode
  PF ==> PrintableForm
  MT ==> SpadMappingType(N)
  NT ==> SpadNamedType(N)

  Exports ==> CoercibleTo(PrintableForm) with
    convert : SExpression -> %

    elt : (%, "name") -> Symbol
    elt : (%, "type") -> MT
    elt : (%, "signature") -> NT
    elt : (%, "condition") -> N

  Implementation ==> add
    import SpadNode
    import Printer
    import SExpression

    Rep := Record(op_name : Symbol, op_info : List(List(SExpression)))

    convert x == x pretend %

    elt (x, "name") ==
      x.op_name

    elt (x, "type") ==
      sig := x.op_info.1
      tl := [parse t for t in destruct sig.1]
      if #sig >= 4 then
        tl := concat(tl, ['constant])
      [rest tl, first tl]$MT

    elt (x, "signature") ==
      [x.name, [x.type]]$NT

    elt (x, "condition") ==
      sig := x.op_info.1
      #sig < 3 or (symbol? sig.3 and symbol sig.3 = 'T) => emptyNode()
      parse sig.3

    coerce x ==
      cond := x.condition
      emptyNode? cond =>
        x.signature :: PF 
      vconcat([x.signature :: PF,
               pile ["when" :: PF, cond :: PF]])

)abbrev domain SPDB SpadDatabase
SpadDatabase() : with
    getOperationList : Symbol -> List SpadOperation
    getCtorArgs : Symbol -> List Symbol
      ++ returns constructor argument names
    getCtorArgsKind : Symbol -> List Boolean
      ++ returns constructor argument kind: true when a type, false when a value
    getCtorCategory : Symbol -> SpadCtorCategory
    getCtorKind : Symbol -> Union("domain", "category", "package")
      ++ returns constructor kind
    getCtorModeMap : Symbol -> SpadCtorModeMap
    getAbbrev : Symbol -> Symbol
    getSourceFile : Symbol -> String
  == add
    import SExpression

    getOperationList t ==
      -- Integer
      sex : SExpression := GETDATABASE(t, 'OPERATIONALIST)$Lisp
      null? sex => error "Unknown constructor!"
      opList := sex pretend List(SExpression)
      [convert op for op in opList]

    getCtorArgsKind t ==
      sex : SExpression := GETDATABASE(t,'COSIG)$Lisp
      null? sex => error "Unknown constructor!"
      (cdr sex) pretend List(Boolean)

    getCtorCategory t ==
      sex : SExpression := GETDATABASE(t, 'CONSTRUCTORCATEGORY)$Lisp
      null? sex => error "Unknown constructor!"
      convert(sex)

    getCtorModeMap t ==
      sex : SExpression := GETDATABASE(t, 'CONSTRUCTORMODEMAP)$Lisp
      null? sex => error "Unknown constructor!"
      convert(sex)

    getCtorArgs t ==
      sex : SExpression := GETDATABASE(t, 'CONSTRUCTORARGS)$Lisp
      sex pretend List(Symbol)

    getCtorKind t ==
      sex : SExpression := GETDATABASE(t, 'CONSTRUCTORKIND)$Lisp
      null? sex => error "Unknown constructor!"
      kind := symbol sex
      kind = 'domain => "domain"
      kind = 'category => "category"
      kind = 'package => "package"
      error "Unknown constructor kind!"

    getAbbrev t ==
      sex : SExpression := GETDATABASE(t, 'ABBREVIATION)$Lisp
      null? sex => error "Unknown constructor!"
      sex pretend Symbol

    getSourceFile t ==
      sex : SExpression := GETDATABASE(t, 'SOURCEFILE)$Lisp
      null? sex => error "Unknown constructor!"
      sex pretend String
